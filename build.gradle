buildscript {
    ext.ktlintVersion = '0.48.0'
    ext.kotlinVersion = '1.9.20'
    ext.androidApplicationVersion = '8.3.1'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidApplicationVersion"
        classpath "com.squareup:javapoet:1.13.0"
        classpath 'com.google.gms:google-services:4.4.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'com.android.application' version "$androidApplicationVersion" apply false
    id 'com.android.library' version "$androidApplicationVersion" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlinVersion" apply false
    id "com.diffplug.spotless" version "6.22.0" // apply false
    id 'com.autonomousapps.dependency-analysis' version "1.17.0" // apply false
    id "com.dorongold.task-tree" version "2.1.0"
    id 'nebula.lint' version '19.0.1'
}

allprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target "v4/**/*.kt"
            targetExclude ".idea/"

            ktlint(ktlintVersion)
                    .userData(["android" : "true"])
                    .editorConfigOverride([
                            "ktlint_standard_enum-entry-name-case" : "disabled",
                            "ktlint_standard_trailing-comma-on-declaration-site": "disabled",
                            "ktlint_standard_no-semi": "disabled",
                    ])
        }
    }
    ext {
        //dependencies shared between both v3 and v4
    }
}


tasks.named('check') {
    dependsOn buildHealth
    dependsOn spotlessApply
}





